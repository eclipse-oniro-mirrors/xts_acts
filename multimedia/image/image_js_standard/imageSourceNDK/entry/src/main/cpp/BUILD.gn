# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/ohos/config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/napi/napi.gni")
import("//foundation/multimedia/image_framework/ide/image_decode_config.gni")

config("config") {
  visibility = [ ":*" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Wunused-variable",
  ]
}

ohos_shared_library("ImageSourceNDKTest") {
  sources = [ "./napi/image_source_test.cpp" ]
  if (target_cpu == "arm") {
    libs = [ "${clang_base_path}/../libcxx-ndk/lib/arm-linux-ohos/c++/libc++_shared.so" ]
  } else if (target_cpu == "arm64") {
    libs = [ "${clang_base_path}/../libcxx-ndk/lib/aarch64-linux-ohos/c++/libc++_shared.so" ]
  } else {
    libs = []
  }
  include_dirs = [
    "../cpp",
    "${resource_management_subsystem}/interfaces/native/resource/include",
  ]
  configs = [ ":config" ]

  deps = [ "$image_subsystem/frameworks/kits/js/common/ndk:image_source_ndk" ]

  external_deps = [
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  output_extension = "so"
  subsystem_name = "multimedia"
  part_name = "image_framework"
}

ohos_shared_library("ImageSourceTest") {
  sources = [
    "./napi/image_source_module_test.cpp",
    "./napi/image_source_native_test.cpp",
  ]
  include_dirs = [
    "../cpp",
    "${resource_management_subsystem}/interfaces/native/resource/include",
    "$image_subsystem/interfaces/kits/native/include",
  ]
  configs = [ ":config" ]

  deps = [ "$image_subsystem/frameworks/kits/js/common/ndk:image_source" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  output_extension = "so"
  subsystem_name = "multimedia"
  part_name = "image_framework"
}
